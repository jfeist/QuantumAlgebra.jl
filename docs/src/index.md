```@meta
CurrentModule = QuantumAlgebra
```
```@meta
DocTestSetup = :( using QuantumAlgebra )
```

# [QuantumAlgebra.jl](https://github.com/jfeist/QuantumAlgebra.jl) - quantum operator algebra in Julia

This package does quantum operator algebra (i.e., algebra with non-commuting
operators) in Julia, supporting bosonic, fermionic, and two-level system
operators, with arbitrary names and indices, as well as sums over any of the
indices. It defines an opinionated canonical form (normal ordering plus some
additional rules) to automatically simplify expressions. It is recommended to
use an interface that can display LaTeX formulas (e.g., Jupyter notebooks) for
convenient output formatting. While there is some documentation, it is not
always kept fully up to date, and it is recommended to look at the latest commit
messages to get an idea about new features etc. You can also check out the
notebooks in the `examples` folder, which can be viewed online with
[nbviewer](https://nbviewer.jupyter.org/github/jfeist/QuantumAlgebra.jl/blob/main/examples/)
and tried out interactively with
[Binder](https://mybinder.org/v2/gh/jfeist/QuantumAlgebra.jl/main?filepath=examples).

# Updates in v0.4.0
This is a major revision with some breaking changes. The backend has been almost
completely rewritten to make the code more efficient when dealing with large
expressions, and the interface has been cleaned up in several places.

### Important changes:
- Canonical normal form is **not** automatically enforced by default. In order
  to transform expressions to normal form, use `normal_form(x)`. Since automatic
  conversion to normal form can be convenient for interactive work, it can be
  enabled with `QuantumAlgebra.auto_normal_form(true)`, or alternatively by
  setting the environment variable `QUANTUMALGEBRA_AUTO_NORMAL_FORM` to `"true"`
  (or any value that `parse(Bool,value)` parses as `true`) before `using
  QuantumAlgebra`.
- The function to obtain expectation values is now `expval(A)` (instead of
  `ExpVal`), and `expval_as_corrs(A)` to express an expectation value through a
  correlator / cumulant expansion, e.g., ‚ü®_AB_‚ü© = ‚ü®_AB_‚ü©<sub>c</sub> -
  ‚ü®_A_‚ü©<sub>c</sub> ‚ü®_B_‚ü©<sub>c</sub>, with corresponding extensions for
  products of more operators. Note that for a single operator, ‚ü®_A_‚ü©<sub>c</sub>
  = ‚ü®_A_‚ü©, but we distinguish the two formally for clarity.
- There is a new function `julia_expression(A)` that converts a QuantumAlgebra
  object to a julia expression, which helps in using QuantumAlgebra to
  programatically derive codes for numerical implementation. The object `A`
  cannot contain any "bare" operators, but only expectation values or
  correlators. See the documentation for more details.
- QuantumAlgebra expressions are now printed in pretty format in the terminal
  etc.

# Overview

The basic functions to create QuantumAlgebra expressions (which are of type
`QuExpr`) are
- `a(inds...)` and `adag(inds...)` for ``a`` and ``a^{‚Ä†}``, the annihilation
  and creation operators for a bosonic mode.
- `f(inds...)` and `fdag(inds...)` for ``f`` and ``f^{‚Ä†}``, the annihilation
  and creation operators for a fermionic mode.
- `œÉx(inds...)`, `œÉy(inds...)`, `œÉz(inds...)` for the Pauli matrices
  ``œÉ^{x,y,z}`` for a two-level system (TLS).
- `œÉp(inds...)`, `œÉm(inds...)` for excitation and deexcitation operators
  ``œÉ^{¬±}`` for a two-level system (TLS).

- **Indices**: All of these functions take an arbitrary number of indices as
  arguments, which can be either integers (1,2,...) or symbolic, where symbolic
  indices must be a single unicode character, with possibly an integer subindex:
  ```jldoctest
  julia> using QuantumAlgebra

  julia> a()
  a()

  julia> adag(:i)
  a‚Ä†(i)

  julia> fdag(1,2,:i_9)
  f‚Ä†(12i‚Çâ)

  julia> œÉx(:i_1, 1, :j, :k_2, :Œº_2, :‚óî_1, :üòÑ_121)
  œÉÀ£(i‚ÇÅ1jk‚ÇÇŒº‚ÇÇ‚óî‚ÇÅüòÑ‚ÇÅ‚ÇÇ‚ÇÅ)
  ```

- You can define your own bosonic/fermionic/two-level system operators with a
  set of macros:
  - `@boson_ops name` defines new functions `$name()` and `$(name)dag()` for
    bosonic species `name`.
  - `@fermion_ops name` defines new functions `$name()` and `$(name)dag()` for
    fermionic species `name`.
  - `@tlsxyz_ops name` defines new functions `$(name)x()`, `$(name)y()` and
    `$(name)z()` for the Pauli matrices for two-level system species `name`.
  - `@tlspm_ops name` defines new functions `$(name)p()` and `$(name)m()` for
    the two-level system excitation and deexcitation operators for species
    `name`.
  ```jldoctest
  julia> @boson_ops b
  (QuantumAlgebra.OpConstructors.b, QuantumAlgebra.OpConstructors.bdag)

  julia> bdag(:k)*b(:i)
  b‚Ä†(k) b(i)
  ```
  Operators with different names are assumed to belong to different "species"
  and always commute.

- `param(name::Symbol,state='n',inds...)` to create a named parameter. `state` must be
  one of `'r'`, `'n'`, or `'c'` for purely real, non-conjugated complex, and
  conjugated complex parameters. More conveniently, parameters can be entered
  with string macros `Pr"name_inds..."` and `Pc"name_inds..."` for real and
  complex parameters:
  ```jldoctest
  julia> Pr"g_i,j_2,k"
  g(ij‚ÇÇk)

  julia> Pr"g_i,j_2,k" == param(:g,'r',:i,:j_2,:k)
  true

  julia> Pc"Œ±_3" == param(:Œ±,3)
  true
  ```

- Arithmetic operations (`*`, `+`, `-`, `^`, `adjoint`=`'`) are supported
  (exponents must be nonnegative integers), with any `Number` types integrating
  automatically.
  ```jldoctest
  julia> 5*adag(:k)*f(3)*œÉx(3)
  5 a‚Ä†(k) f(3) œÉÀ£(3)

  julia> (5//3+4im) * adag(:k)*f(3)*œÉx(3) + 9.4
  9.4 + (5//3+4i) a‚Ä†(k) f(3) œÉÀ£(3)

  julia> (a(:i)*f(:k))'
  f‚Ä†(k) a‚Ä†(i)
  ```
  If you need a bare number as a QuantumAlgebra expression, you can use
  `x*one(QuExpr)` (or `one(A)`, where `A` is any `QuExpr`).

- `‚àë(ind,A::QuExpr)` to represent an analytic sum over index `ind`. Since summed
  indices have no semantic meaning, the index within the expression gets
  replaced by a special numbered sum index `#·µ¢`, with `i=1,2,...`.
  ```jldoctest
  julia> ‚àë(:i,a(:i))
  ‚àë‚ÇÅ a(#‚ÇÅ)
  ```

- `normal_form(A::QuExpr)` converts an expression to a well-defined "canonical"
  order. To achieve this canonical form, relevant commutators etc are used, so
  an expression written as a single product can turn into a sum of expressions.
  The order is essentially normal ordering (creation before annihilation
  operators, with œÉÀ£ ∏·∂ª in the middle), with some additional conventions to make
  the normal form (hopefully) unique. In some contexts (e.g., interactive work),
  it can be convenient to automatically transform all expressions to normal
  form. This can be enabled by calling `QuantumAlgebra.auto_normal_form(true)`,
  or alternatively by setting the environment variable
  `QUANTUMALGEBRA_AUTO_NORMAL_FORM` to `"true"` (or any value that
  `parse(Bool,value)` parses as `true`) before `using QuantumAlgebra`.

- `expval(A::QuExpr)` to represent an expectation value.
  ```jldoctest
  julia> expval(adag(:j)*a(:i))
  ‚ü®a‚Ä†(j) a(i)‚ü©
  ```

- `expval_as_corrs(A::QuExpr)` to represent an expectation value through its
  correlators, i.e., a cumulant expansion.
  ```jldoctest
  julia> expval_as_corrs(adag(:j)*a(:i))
  ‚ü®a‚Ä†(j)‚ü©c ‚ü®a(i)‚ü©c  + ‚ü®a‚Ä†(j) a(i)‚ü©c
  ```

- `comm(A::QuExpr,B::QuExpr)` to calculate the commutator ``[A,B] = AB - BA``.
  ```jldoctest
  julia> comm(a(),adag())
  -a‚Ä†() a() + a() a‚Ä†()

  julia> normal_form(comm(a(),adag()))
  1
  ```

- `Avac(A)` and `vacA(A)` simplify operators by assuming they are applied to the
  vacuum from the left or right, respectively. To be precise, `Avac(A)` returns
  ``A'`` such that ``A|0‚ü© = A'|0‚ü©``, while `vacA(A)` does the same for ``‚ü®0|A``.
  These functions automatically apply `normal_form` to assure that the operators
  are simplified as much as possible. Note that "vacuum" for two-level systems
  is interpreted as the lower state, ``œÉ^{z}|0‚ü© = -|0‚ü©``.
  ```jldoctest
  julia> Avac(a())
  0

  julia> Avac(a(:i)*adag(:j))
  Œ¥(ij)

  julia> Avac(a()*adag()*adag())
  2 a‚Ä†()

  julia> vacA(a()*adag()*adag())
  0

  julia> Avac(œÉx())
  œÉÀ£()

  julia> Avac(œÉz())
  -1
  ```

- `vacExpVal(A,S=1)` calculates the vacuum expectation value ``‚ü®0|S^{‚Ä†}AS|0‚ü©``,
  i.e., the expectation value ``‚ü®œà|A|œà‚ü©`` for the state defined by ``|œà‚ü©=S|0‚ü©``.
  The result is guaranteed to not contain any operators.
  ```jldoctest
  julia> vacExpVal(adag()*a())
  0

  julia> vacExpVal(adag()*a(), adag()^4/sqrt(factorial(4)))
  4.000000000000001

  julia> vacExpVal(adag()*a(), adag()^4/sqrt(factorial(big(4))))
  4

  julia> vacExpVal(œÉx())
  0
  ```

- `julia_expression(A)` to obtain a julia expression that can be used to
  automatically build codes implementing equations derived with QuantumAlgebra.
  Every expectation value or correlator is treated as a separate array. Daggers
  are represented as `·¥¥`, which are valid identifiers that can appear in the
  array names. Note that expectation values and correlators are not
  distinguished, so it is best to have all expressions use the same kind.
  ```jldoctest
  julia> julia_expression(expval_as_corrs(adag(:j)*a(:i)))
  :(a·¥¥[j] * a[i] + a·¥¥a[j, i])
  ```
  Also note that expressions are always treated as arrays, even if they have no
  indices (which gives zero-dimensional arrays). If you are working with scalar
  quantities exclusively, it might be useful to clean up the resulting
  expression (e.g., use `MacroTools` to remove the `[]`).
  ```jldoctest
  julia> julia_expression(expval(adag()*a()*œÉx()))
  :(a·¥¥aœÉÀ£[])
  ```

## Citing

If you use QuantumAlgebra in academic work, we would appreciate a citation. See
[`CITATION.bib`](https://github.com/jfeist/QuantumAlgebra.jl/blob/main/CITATION.bib) for the relevant references.
